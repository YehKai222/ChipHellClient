buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4+'
    }
}
repositories {
    mavenCentral()
    mavenLocal()
}
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName "com.fei_ke.chiphellclient"
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'


    defaultConfig {
        versionCode 12
        versionName "1.2.5"
        minSdkVersion 14
        targetSdkVersion 21
    }
    productFlavors {
        Develop {}
        ChipHellForum {}
        GooglePlay {}
        Baidu {}
        WanDouJia {}

    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    signingConfigs {
        chh {
            storeFile file("../" + chh_storeFile)
            storePassword chh_storePassword
            keyAlias chh_keyAlias
            keyPassword chh_keyPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.chh
        }
        debug {
            signingConfig signingConfigs.chh
        }
    }
    lintOptions {
//        abortOnError false
    }

    applicationVariants.all { variant ->
        if (variant.productFlavors[0] == null) {
            variant.outputs[0].outputFile = new File(variant.outputs[0].outputFile.parent,
                    project.name + "_"
                            + variant.buildType.name + "_"
                            + android.defaultConfig.versionName + ".apk")
        } else {
            variant.outputs[0].outputFile = new File(variant.outputs[0].outputFile.parent,
                    project.name + "_"
                            + variant.productFlavors[0].name + "_"
                            + variant.buildType.name + "_"
                            + variant.mergedFlavor.versionName + ".apk")
        }
    }

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile 'com.nostra13.universalimageloader:universal-image-loader:latest.integration'
//        compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
        compile 'pl.droidsonroids.gif:android-gif-drawable:latest.integration'
        compile 'org.androidannotations:androidannotations:latest.integration'
        compile 'com.android.support:support-v4:22.1.0'
        compile 'com.android.support:appcompat-v7:22.1.0'
        compile 'com.sothree.slidinguppanel:library:latest.integration'
        compile 'com.diegocarloslima:fgelv:0.1.+@aar'
        compile 'de.greenrobot:eventbus:latest.integration'
        compile 'org.jsoup:jsoup:latest.integration'
        compile 'com.umeng.analytics:analytics:latest.integration'

        apt "org.androidannotations:androidannotations:latest.integration"
        compile project(':libraries:ActionBarPullToRefresh')
        compile project(':libraries:AsyncHttpClient')
        compile project(':libraries:QuickReturn')
//        compile project(':libraries:SwipeBack')
    }
}
